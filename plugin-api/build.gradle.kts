plugins {
	id("com.android.library")
	id("kotlin-android")
	id("kotlin-parcelize")
	alias(libs.plugins.binary.compatibility.validator)
}

android {
	namespace = "com.itsaky.androidide.plugins.api"
	compileSdk = 36

	defaultConfig {
		minSdk = 28
	}

	compileOptions {
		sourceCompatibility = JavaVersion.VERSION_17
		targetCompatibility = JavaVersion.VERSION_17
	}
}

kotlin {
	compilerOptions {
		jvmTarget.set(org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_17)
		// Pin to match the on-device Kotlin compiler
		apiVersion.set(org.jetbrains.kotlin.gradle.dsl.KotlinVersion.KOTLIN_2_1)
		languageVersion.set(org.jetbrains.kotlin.gradle.dsl.KotlinVersion.KOTLIN_2_1)
	}
}

apiValidation {
	ignoredClasses.add("com.itsaky.androidide.plugins.api.BuildConfig")
	nonPublicMarkers.add("com.itsaky.androidide.plugins.base.InternalPluginApi")
}

dependencies {
	// Only include Android context for basic Android functionality
	compileOnly("androidx.appcompat:appcompat:1.6.1")
	compileOnly("androidx.fragment:fragment-ktx:1.6.2")
	compileOnly("com.google.android.material:material:1.11.0")

	api("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.9.0")

	// Test dependencies
	testImplementation("junit:junit:4.13.2")
}

tasks.register<Copy>("createPluginApiJar") {
	dependsOn("assembleRelease")
	from(layout.buildDirectory.file("intermediates/aar_main_jar/release/syncReleaseLibJars/classes.jar"))
	into(layout.buildDirectory.dir("libs"))
	rename { "plugin-api-1.0.0.jar" }
}
